plugins {
    id 'org.sonarqube' version '5.1.0.4882'
    id 'jacoco'
}

ext {
    springBootVersion = '3.2.0'
    dependencyManagementVersion = '1.1.4'
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    jacoco {
        toolVersion = "0.8.11"
    }

    test {
        ignoreFailures = true
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            html.required = true
            csv.required = false
        }
    }
}

sonar {
    properties {
        property 'sonar.projectKey', 'hafeezilias_softwarequality'
        property 'sonar.organization', 'hafeezilias'
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.projectName', 'DDD E-Commerce Microservices'
        property 'sonar.projectVersion', '2.0-SNAPSHOT'
        property 'sonar.sourceEncoding', 'UTF-8'
        property 'sonar.java.source', '17'
        property 'sonar.coverage.jacoco.xmlReportPaths', '**/build/reports/jacoco/test/jacocoTestReport.xml'
        property 'sonar.junit.reportPaths', '**/build/test-results/test'
        property 'sonar.java.binaries', '**/build/classes/java/main'
        property 'sonar.exclusions', '**/test/**, **/build/**, **/resources/**'
    }
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn = subprojects.test
    additionalSourceDirs.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.from = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(subprojects.sourceSets.main.output)
    executionData.from = files(subprojects.jacocoTestReport.executionData)

    reports {
        xml.required = true
        xml.destination = file("${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml")
        html.required = true
        html.destination = file("${buildDir}/reports/jacoco/jacocoRootReport/html")
    }

    onlyIf = {
        true
    }

    doFirst {
        executionData = files(executionData.findAll {
            it.exists()
        })
    }
}
